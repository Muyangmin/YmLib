logger.debug("--------------> Injecting dependencies")
if (project.plugins.hasPlugin("com.android.application")
        || project.plugins.hasPlugin("com.android.library")) {

    apply plugin: "kotlin-android"
    apply plugin: "kotlin-android-extensions"
    apply plugin: 'kotlin-kapt'
    apply plugin: 'org.jetbrains.dokka'
    apply from: '../dependencies.gradle'

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
    }

    android {
        compileSdkVersion versionConfig.compileSdkVersion

        defaultConfig {
            minSdkVersion versionConfig.minSdkversion
            targetSdkVersion versionConfig.targetSdkVersion
            versionCode versionConfig.libraryVersionCode
            versionName versionConfig.libraryVersionName
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        androidExtensions {
            experimental = true
        }

        lintOptions {
            abortOnError false
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }

    dependencies {
        implementation deps.kotlinLib

        testImplementation testDeps.junit
        androidTestImplementation testDeps.runner
        androidTestImplementation testDeps.espresso
    }

    if (project.plugins.hasPlugin("com.android.library")) {
        apply plugin: 'com.novoda.bintray-release'
        publish {
            userOrg = "muyangmin"
            repoName = 'org.mym'
            groupId = 'org.mym.ymlib'
            publishVersion = versionConfig.libraryVersionName
            desc = 'A widely applicable, easy to use, powerful but light weight foundation library for most modern Android applications.'
            website = 'https://github.com/Muyangmin/YmLib'
        }
    }
}
logger.debug("--------------> Injected dependencies.")